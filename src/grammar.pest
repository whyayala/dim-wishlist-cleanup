WHITESPACE = _{ " " }

id = {ASCII_DIGIT+}
ids = _{id ~ ("," ~ id)+}

wishlist_title = {"title:" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}
wishlist_description = {"description:" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}

start_of_roll = _{"dimwishlist:"}

item_key = _{"item="}
item = _{item_key ~ id}

perks_key = _{"perks="}
perks = {perks_key ~ ids}

notes_key = _{"#notes:"}

notes = {(!notes_key ~ !NEWLINE ~ ANY)* }

roll = { start_of_roll ~ item ~ "&" ~ perks ~ (notes_key ~ notes)? ~ NEWLINE}
rolls = {roll+}

comment = _{"//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE}
weapon_name = {comment}
weapon_notes = {comment}

weapon_roll = {weapon_name ~ weapon_notes ~ rolls}

wishlist = {wishlist_title ~ wishlist_description ~ (NEWLINE ~ weapon_roll)*}