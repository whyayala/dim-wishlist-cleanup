voltron = _{voltron_title ~ voltron_description ~ (NEWLINE | wishlist)+}
wishlist = {start_of_wishlist ~ (NEWLINE* ~ weapon_roll)+}

voltron_title = _{title}
voltron_description = _{description}

wishlist_title = {title}
wishlist_description = {description}
wishlist_notes = {notes_key ~ text}

text = {(!NEWLINE ~ ANY)* ~ NEWLINE}

title = _{(AT | "//")? ~ "title:" ~ text}
description = _{(AT | "//")? ~ "description:" ~ text}

start_of_roll = _{"dimwishlist:"}

item_key = _{"item="}
item = _{item_key ~ id}

perks_key = _{"perks="}
perks = {perks_key ~ ids}

notes_key = _{("#" | "@" | "//")? ~ "notes:"}

notes = {(!notes_key ~ !NEWLINE ~ ANY)* }

roll = { start_of_roll ~ item ~ "&" ~ perks ~ (notes_key ~ notes)? ~ NEWLINE}
rolls = {roll*}

comment = _{("/" | "(") ~ (!start_of_roll ~ text)+}
weapon_notes = {comment*}

weapon_roll = {weapon_notes ~ rolls ~ NEWLINE}

source_comment = {("//taken from" | "// taken from" | "// originally" | "// sourced from") ~ (!NEWLINE ~ ANY)* ~ NEWLINE*}

start_of_wishlist = {
	(
		("decription:" ~ text) | wishlist_title | wishlist_description | source_comment | wishlist_notes | NEWLINE
    )*
}


ids = _{id ~ ("," ~ id)+}
id = {ASCII_DIGIT+}

AT = _{"@"}
WHITESPACE = _{ " " }